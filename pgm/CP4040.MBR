      *=========================================================================                    
      *                                                                                             
      *   Gestion du Plan comptable.                                                                
      *                                                                                             
      *=========================================================================                    
      *                                                                                             
      *  Détail des indicateurs utilisés.                                                           
      *                                                                                             
      *    *IN01 _  Aide.                                                                           
      *    *IN02 _  Touche de fonction incorrecte.                                                  
      *    *IN03 _  Exit.                                                                           
      *    *IN04 _  Invite.                                                                         
      *    *IN05 _  Réafficher.                                                                     
      *    *IN06 _  Créer.                                                                          
      *    *IN12 _  Annuler.                                                                        
      *    *IN15 _  Options d'édition.                                                              
      *    *IN21 _  QUSCMDLN.                                                                       
      *    *IN22 _  DSPOBJD.                                                                        
      *    *IN25 _  SFLDSP, SFLDSPCTL                                                               
      *    *IN26 _  Protection SFLDSP.                                                              
      *    *IN27 _  SFLEND.                                                                         
      *    *IN28 _  ROLLUP.                                                                         
      *    *IN29 _  ROLLDOWN.                                                                       
      *    *IN30 _  SFLNXTCHG.                                                                      
      *    *IN31 _  Erreur zone option.                                                             
      *    *IN45 _  SFLDSP, SFLDSPCTL                                                               
      *    *IN47 _  SFLEND.                                                                         
      *                                                                                             
      *=========================================================================                    
      *                                                                                             
      *  Constitution des variables:                                                                
      *                                                                                             
      *     VAR   _  Variable fichier.                                                              
      *    àVAR   _  Variable paramètre.                                                            
      *    $VAR   _  Variable écran.                                                                
      *    £VAR   _  Variable programme.                                                            
      *                                                                                             
      *=========================================================================                    
     H DATEDIT(*DMY)                                                                                
      *                                                                                             
      * Plan comptable.                                                                             
      *                                                                                             
     FAFGLI04L  IF   E           K DISK                                                             
      *                                                                                             
      * Grand livre.                                                                                
      *                                                                                             
     FAFGLI01L  IF   E           K DISK                                                             
     F                                     RENAME(AFGLIF01:AFGLI01)                                 
      *                                                                                             
      * Messages.                                                                                   
      *                                                                                             
     FAFMSG02L  IF   E           K DISK                                                             
      *                                                                                             
      * DSPF.                                                                                       
      *                                                                                             
     FCP4040FM  CF   E             WORKSTN INFDS(INFO)                                              
     F                                     SFILE(SF:$RANG)                                          
      *                                                                                             
      *=========================================================================                    
     D MSG             S             79    DIM(1) CTDATA PERRCD(1)                                  
      *=========================================================================                    
      *                                                                                             
     D QDSPGM         SDS                                                                           
     D  £PRG                   1     10                                                             
     D  £JOB                 244    253                                                             
     D  £USRX                254    263                                                             
     D  £PARM            *PARMS                                                                     
      *                                                                                             
     D INFO            DS                                                                           
     D  £XY                  370    371B 0                                                          
     D  £DEF                 378    379B 0                                                          
      *                                                                                             
     D                 DS                                                                           
     D  £IMSG                  1     79                                                             
     D  £SMSG                  9     12                                                             
      *                                                                                             
     D                 DS                                                                           
     D  £DTEND                 1      8                                                             
     D  £JOU1                  1      2                                                             
     D  £MOI1                  3      4                                                             
     D  £ANN1                  5      8                                                             
      *                                                                                             
     D                 DS                                                                           
     D  £DTENV                 1      8                                                             
     D  £ANN2                  1      4                                                             
     D  £MOI2                  5      6                                                             
     D  £JOU2                  7      8                                                             
      *                                                                                             
     D                UDS                                                                           
     D  ZCCSOC               350    351                                                             
      *                                                                                             
      *=========================================================================                    
      * TRAITEMENT.                                                                                 
      *=========================================================================                    
      *                                                                                             
      * Gestion zone message.                                                                       
      *                                                                                             
     C                   EXSR      àMSG                                                             
     C                   MOVEL     £IMSG         $M                                                 
      *                                                                                             
      * Affichage du sous-fichier.                                                                  
      *                                                                                             
     C                   WRITE     BA                                                               
     C                   WRITE     VD                                                               
     C                   EXFMT     CT                                                               
      *                                                                                             
      * Positionnement sur la page en cours.                                                        
      *                                                                                             
     C                   Z-ADD     £DEF          $RANG                                              
     C                   Z-ADD     £DEF          £DEF2             4 0                              
      *                                                                                             
      * Traitement touches de fonction, touche ENTREE.                                              
      *                                                                                             
     C                   SELECT                                                 -+                  
      *                                                     !                                       
      * F1=Aide.                                            !                                       
      *                                                     !                                       
     C     *IN01         WHENEQ    *ON                                           !                  
     C                   CALL      'AFHELP'                                      !                  
     C                   PARM                    £PRG                            !                  
     C                   PARM      'C1'          £FMT              2             !                  
      *                                                     !                                       
      * Touche de fonction incorrecte.                      !                                       
      *                                                     !                                       
     C     *IN02         WHENEQ    *ON                                           !                  
     C                   Z-ADD     2             £MSG                            !                  
      *                                                     !                                       
      * F3=Exit.                                            !                                       
      *                                                     !                                       
     C     *IN03         WHENEQ    *ON                                           !                  
     C                   SETON                                        LR         !                  
      *                                                     !                                       
      * F5=Réafficher.                                      !                                       
      *                                                     !                                       
     C     *IN05         WHENEQ    *ON                                           !                  
     C                   EXSR      àF5                                           !                  
      *                                                     !                                       
      * F15=Options d'édition.                              !                                       
      *                                                     !                                       
     C     *IN15         WHENEQ    *ON                                           !                  
     C                   EXSR      àF15                                          !                  
      *                                                     !                                       
      * F21=QUSCMDLN                                        !                                       
      *                                                     !                                       
     C     *IN21         WHENEQ    *ON                                           !                  
     C                   CALL      'QUSCMDLN'                                    !                  
      *                                                     !                                       
      * F22=DSPOBJD.                                        !                                       
      *                                                     !                                       
     C     *IN22         WHENEQ    *ON                                           !                  
     C                   CALL      'AFRENSPG'                                    !                  
     C                   PARM                    QDSPGM                          !                  
      *                                                     !                                       
      * F24=Autres touches.                                 !                                       
      *                                                     !                                       
     C     *IN24         WHENEQ    *ON                                           !                  
      *                                                     !                                       
      * ROLLUP.                                             !                                       
      *                                                     !                                       
     C     *IN28         WHENEQ    *ON                                           !                  
     C                   EXSR      àF28                                          !                  
      *                                                     !                                       
      * ROLLDOWN                                            !                                       
      *                                                     !                                       
     C     *IN29         WHENEQ    *ON                                           !                  
     C                   EXSR      àF29                                          !                  
      *                                                     !                                       
      * Sinon contrôle des saisies.                         !                                       
      *                                                     !                                       
     C                   OTHER                                                   *                  
     C                   EXSR      àENTER                                        !                  
     C                   ENDSL                                                  -+                  
      *                                                                                             
      *=========================================================================                    
      * *INZSR: initialisation.                                                                     
      *=========================================================================                    
      *                                                                                             
     C     *INZSR        BEGSR                                                                      
      *                                                                                             
      * Réception des paramètres.                                                                   
      *                                                                                             
     C     *ENTRY        PLIST                                                                      
     C                   PARM                    ààSOC             2            Société             
     C                   PARM                    àMTPR             1                                
     C                   PARM                    àMONT            11                                
     C                   PARM                    àSENS             1                                
     C                   PARM                    ààRET             1            Retour              
      *                                                                                             
      * Description de la clé de AFGLI04L.                                                          
      *                                                                                             
     C     £KGLI4        KLIST                                                                      
     C                   KFLD                    àSOC              2                                
     C                   KFLD                    £DEB             11 2                              
     C                   KFLD                    £CRE             11 2                              
      *                                                                                             
      * Description de la clé de AFGLI01L                                                           
      *                                                                                             
     C     KGLI01        KLIST                                                                      
     C                   KFLD                    àSOC                           =Société.           
     C                   KFLD                    £PERI             6                        ptabl   
     C                   KFLD                    £NECR             5 0          =N° ecriture        
     C                   KFLD                    £RECR             5 0          =Rang écriture.     
      *                                                                                             
      * Description de la clé de AFMSG02L.                                                          
      *                                                                                             
     C     £KMSG         KLIST                                                                      
     C                   KFLD                    £FIL             10                                
     C                   KFLD                    £MSG              4 0                              
      *                                                                                             
      * Initialisation de la clé de AFMSG02L.                                                       
      *                                                                                             
     C                   MOVE      *BLANK        £FIL                                               
     C                   MOVEL     'CP1000  '    £FIL                                               
     C                   MOVE      '  '          £FIL                                               
     C                   Z-ADD     *ZERO         £MSG                                               
      *                                                                                             
      * Société.                                                                                    
      *                                                                                             
     C     £PARM         IFGE      1                                            -+                  
     C                   MOVEL     ààSOC         àSOC                            !                  
     C                   ELSE                                                    *                  
     C                   MOVEL     ZCCSOC        àSOC                            !                  
     C                   END                                                    -+                  
      *                                                                                             
      * Chargement des zones.                                                                       
      *                                                                                             
     C     £PARM         IFGE      2                                            -+                  
     C                   MOVE      àMTPR         $MTPR                           !                  
     C                   END                                                    -+                  
     C     £PARM         IFGE      3                                            -+                  
     C                   MOVE      àMONT         $MONT                           !                  
     C                   END                                                    -+                  
     C     £PARM         IFGE      4                                            -+                  
     C                   MOVE      àSENS         $SENS                           !                  
     C                   END                                                    -+                  
     C     £PARM         IFGE      5                                            -+                  
     C                   MOVE      *ON           ààRET                           !                  
     C                   END                                                    -+                  
      *                                                                                             
      * Conversion date du jour.                                                                    
      *                                                                                             
     C                   MOVEL     *DATE         £UDATE                                             
      *                                                                                             
      * Extraction période comptable.                                                               
      *                                                                                             
     C                   CALL      'CL1013C'                                                        
     C                   PARM                    àSOC                                               
     C                   PARM                    àDEB              6                                
     C                   PARM                    àFIN              6                                
     C                   PARM      *OFF          àRET2             1                                
     C     àRET2         IFEQ      *OFF                                                             
     C                   MOVEL     àDEB          $EXER                                              
     C                   MOVE      '/       '    $EXER                                              
     C                   MOVE      àFIN          $EXER                                              
     C                   ELSE                                                                       
     C                   MOVEL     '????????'    $EXER                                              
     C                   END                                                                        
      *                                                                                             
      * Préparation du sous fichier.                                                                
      *                                                                                             
     C                   EXSR      àRECH                                                            
      *                                                                                             
     C                   ENDSR                                                                      
      *                                                                                             
      *=========================================================================                    
      * àMSG: extraction d'un message.                                                              
      *=========================================================================                    
      *                                                                                             
     C     àMSG          BEGSR                                                                      
     C     £MSG          IFEQ      *ZERO                                        -+                  
     C                   Z-ADD     1             £MSG                            !                  
     C                   END                                                    -+                  
     C     £KMSG         CHAIN     AFMSG02L                           50                            
     C     *IN50         IFEQ      *OFF                                         -+                  
     C                   MOVE      INTMSG        £IMSG                           !                  
     C                   ELSE                                                    *                  
     C                   MOVE      MSG(1)        £IMSG                           !                  
     C                   MOVE      £MSG          £SMSG                           !                  
     C                   END                                                    -+                  
     C                   Z-ADD     *ZERO         £MSG                                               
     C                   ENDSR                                                                      
      *                                                                                             
      *=========================================================================                    
      * àINIT: Initialisation du sous-fichier.                                                      
      *=========================================================================                    
      *                                                                                             
     C     àINIT         BEGSR                                                                      
      *                                                                                             
      * Effacement du sous-fichier.                                                                 
      *                                                                                             
     C                   SETON                                        25                            
     C                   WRITE     CT                                                               
     C                   SETOFF                                       252627                        
      *                                                                                             
      * Initialisation variable pour valeur dernier numéro de rang.                                 
      *                                                                                             
     C                   Z-ADD     *ZERO         £EOSF             4 0                              
      *                                                                                             
      * Emplissage de la première page.                                                             
      *                                                                                             
     C                   Z-ADD     $MONT         £MONTS           11 2                              
     C                   EXSR      àROLL                                                            
      *                                                                                             
     C                   ENDSR                                                                      
      *                                                                                             
      *=========================================================================                    
      * àROLL: Emplissage d'une page.                                                               
      *=========================================================================                    
      *                                                                                             
     C     àROLL         BEGSR                                                                      
      *                                                                                             
      * Initialisation du numéro de rang.                                                           
      *                                                                                             
     C                   Z-ADD     £EOSF         $RANG                                              
      *                                                                                             
      * Positionnement dans le fichier.                                                             
      *                                                                                             
     C     $SENS         IFEQ      'D'                                          -+                  
     C                   Z-ADD     £MONTS        £DEB                            !                  
     C                   Z-ADD     *ZEROS        £CRE                            !                  
     C                   ELSE                                                   -+                  
     C                   Z-ADD     £MONTS        £CRE                            !                  
     C                   Z-ADD     *ZEROS        £DEB                            !                  
     C                   ENDIF                                                  -+                  
      *                                                                                             
     C     £SETLL        IFEQ      *ON                                          --+                 
     C     £KGLI4        SETLL     AFGLI04L                                       !                 
     C                   MOVE      *OFF          £SETLL                           !                 
     C                   END                                                    --+                 
      *                                                                                             
      * Initialisation des indicateurs.                                                             
      *                                                                                             
     C                   MOVEA     *OFF          *IN(30)                                            
      *                                                                                             
     C     £MONTS        IFEQ      *ZEROS                                                           
     C                   MOVE      *ON           *IN81                                              
     C                   ELSE                                                                       
     C                   MOVE      *OFF          *IN81                                              
     C                   ENDIF                                                                      
      *                                                                                             
      * Initialisation zone option.                                                                 
      *                                                                                             
     C                   MOVE      *BLANK        $OPT                                               
      *                                                                                             
      * Emplissage d'une page du sous-fichier.                                                      
      *                                                                                             
     C                   Z-ADD     *ZERO         £DO               2 0                              
     C     £DO           DOWLT     9                                            ---+                
      *                                                       !                                     
      * Lecture.                                              !                                     
      *                                                       !                                     
     C                   READ      AFGLI04L                               27       !                
      *                                                       !                                     
      * Rupture.                                              !                                     
      *                                                       !                                     
     C                   EXSR      àRUPT                                           !                
      *                                                       !                                     
      * Ecriture.                                             !                                     
      *                                                       !                                     
     C     *IN27         IFEQ      *OFF                                         --+!                
      *                                                      !!                                     
      * Mémorisation des zones clés.                         !!                                     
      *                                                      !!                                     
     C     GLMTDT        SUB       GLMTCT        £MONTS           11 2            !!                
      *                                                      !!                                     
      * Ecriture de l'enregistrement.                        !!                                     
      *                                                      !!                                     
     C     £WRITE        IFEQ      *OFF                                         -+!!                
     C                   ADD       1             £DO                             !!!                
     C                   ADD       1             $RANG                           !!!                
     C                   EXSR      àLOAD                                         !!!                
     C                   WRITE     SF                                            !!!                
     C                   END                                                    -+!!                
     C                   END                                                    --+!                
     C                   END                                                    ---+                
      *                                                                                             
      * Lecture d'avance pour conditionnement SFLEND.                                               
      *                                                                                             
     C     *IN27         IFEQ      *OFF                                         ---+                
     C                   MOVE      *ON           £WRITE                            !                
     C     *IN27         DOWEQ     *OFF                                         --+!                
     C     £WRITE        ANDEQ     *ON                                            !!                
     C                   READ      AFGLI04L                               27      !!                
     C     *IN27         IFEQ      *OFF                                         -+!!                
     C                   EXSR      àRUPT                                         !!!                
     C                   END                                                    -+!!                
     C                   READP     AFGLI04L                               27      !!                
     C                   END                                                    --+!                
     C                   END                                                    ---+                
      *                                                                                             
      * Mémorisation du dernier numéro de rang.                                                     
      *                                                                                             
     C                   Z-ADD     $RANG         £EOSF                                              
      *                                                                                             
      * Protection affichage du sous-fichier.                                                       
      *                                                                                             
     C     $RANG         COMP      *ZERO                                  26                        
      *                                                                                             
      * Positionnement curseur sur la première ligne.                                               
      *                                                                                             
     C     £EOSF         DIV       9             £REST             4 0                              
     C                   MVR                     £REST                                              
     C     9             SUB       £REST         £REST                                              
     C                   SUB       8             £REST                                              
     C     £REST         IFGT      *ZERO                                        -+                  
     C                   SUB       8             $RANG                           !                  
     C                   ELSE                                                    *                  
     C                   ADD       £REST         $RANG                           !                  
     C                   END                                                    -+                  
      *                                                                                             
     C                   ENDSR                                                                      
      *                                                                                             
      *=========================================================================                    
      * àRUPT:                                                                                      
      *=========================================================================                    
      *                                                                                             
     C     àRUPT         BEGSR                                                                      
     C                   MOVE      *OFF          £WRITE            1                                
     C     GLCSOC        IFNE      àSOC                                         -+                  
     C                   SETON                                        27         !                  
     C                   END                                                    -+                  
     C     $MTPR         IFEQ      'O'                                          -+                  
     C     GLMTDT        IFNE      $MONT                                         !                  
     C     GLMTCT        ANDNE     $MONT                                         !                  
     C                   SETON                                        27         !                  
     C                   END                                                     !                  
     C                   END                                                    -+                  
     C     *IN27         IFEQ      *ON                                          -+                  
     C                   Z-ADD     *HIVAL        £DO                             !                  
     C                   END                                                    -+                  
     C                   ENDSR                                                                      
      *                                                                                             
      *=========================================================================                    
      * àLOAD: chargement ligne sous-fichier.                                                       
      *=========================================================================                    
      *                                                                                             
     C     àLOAD         BEGSR                                                                      
     C     GLMTDT        SUB       GLMTCT        $MTSF                                              
     C                   MOVEL     GLDTCP        £DTENV                                             
     C                   EXSR      àCVTE                                                            
     C                   MOVEL     £DTEND        £DTSF                                              
     C                   MOVEL     GLLIBL        $LISF                                              
     C                   MOVEL     GLNECR        £NECRS                                             
     C                   MOVEL     £DTEND        £DTCPS                                             
     C                   MOVEL     GLRECR        £RECRS                                             
     C                   MOVEL     GLDTCP        £PERIS                                             
     C                   ENDSR                                                                      
      *                                                                                             
      *=========================================================================                    
      * àENTER: Contrôle des saisies.                                                               
      *=========================================================================                    
      *                                                                                             
     C     àENTER        BEGSR                                                                      
      *                                                                                             
     C                   MOVEA     *OFF          *IN(60)                                            
      *                                                                                             
     C                   SELECT                                                 --+                 
      *                                                      !                                      
      * Recherche.                                           !                                      
      *                                                      !                                      
     C     $MTPR         WHENNE    £MTP2                                          !                 
     C     $MONT         ORNE      £MON2                                          !                 
     C     $SENS         ORNE      £SEN2                                          !                 
     C                   EXSR      àRECH                                          !                 
      *                                                      !                                      
      * Contrôle sous fichier.                               !                                      
      *                                                      !                                      
     C                   OTHER                                                    *                 
     C     *IN26         COMP      *ON                                    LR      !                 
     C     *INLR         IFEQ      *OFF                                         -+!                 
     C                   READC     SF                                     LR     !!                 
     C                   END                                                    -+!                 
     C     *INLR         IFEQ      *OFF                                         -+!                 
     C                   SETOFF                                       50         !!                 
     C                   EXSR      àSFL                                          !!                 
     C                   END                                                    -+!                 
     C     *INLR         IFEQ      *ON                                          -+!                 
     C     £PARM         ANDGE     5                                             !!                 
     C                   MOVE      *OFF          ààRET                           !!                 
     C                   END                                                    -+!                 
     C                   END                                                    --+                 
      *                                                                                             
     C                   ENDSR                                                                      
      *                                                                                             
      *=========================================================================                    
      * àRECH: recherche.                                                                           
      *=========================================================================                    
      *                                                                                             
     C     àRECH         BEGSR                                                                      
      *                                                                                             
     C                   MOVE      *ON           £SETLL            1                                
      *                                                                                             
      * MTPR.                                                                                       
      *                                                                                             
     C     $MTPR         IFNE      'O'                                                              
     C     $MTPR         ANDNE     'N'                                                              
     C                   MOVEL     'N'           $MTPR                                              
     C                   ENDIF                                                                      
     C                   MOVE      $MTPR         £MTP2             1                                
      *                                                                                             
      * SENS.                                                                                       
      *                                                                                             
     C     $SENS         IFNE      'D'                                                              
     C     $SENS         ANDNE     'C'                                                              
     C                   MOVEL     'D'           $SENS                                              
     C                   ENDIF                                                                      
     C                   MOVE      $SENS         £SEN2             1                                
      *                                                                                             
      * $MONT                                                                                       
      *                                                                                             
     C                   Z-ADD     $MONT         £MON2            11 2                              
      *                                                                                             
     C                   EXSR      àINIT                                                            
      *                                                                                             
     C                   ENDSR                                                                      
      *                                                                                             
      *=========================================================================                    
      * àSFL: Contrôle des saisies sous-fichier.                                                    
      *=========================================================================                    
      *                                                                                             
     C     àSFL          BEGSR                                                                      
      *                                                                                             
      * Initialisation code retour.                                                                 
      *                                                                                             
     C                   MOVE      *OFF          àRET              1                                
      *                                                                                             
      * Initialisation variable pour réaffichage.                                                   
      *                                                                                             
     C                   Z-ADD     *ZERO         £REAF             4 0                              
      *                                                                                             
      * Tant qu'il y a des enregistrements modifiés.                                                
      *                                                                                             
     C     *IN50         DOWEQ     *OFF                                         ---+                
      *                                                       !                                     
      * Formatage zone option.                                !                                     
      *                                                       !                                     
     C     $OPT          IFNE      *BLANK                                       --+!                
     C                   MOVEL     $OPT          £OPTA             1              !!                
     C                   MOVE      $OPT          £OPTB             1              !!                
     C     £OPTB         IFEQ      *BLANK                                       -+!!                
     C                   MOVE      *BLANK        $OPT                            !!!                
     C                   MOVE      £OPTA         $OPT                            !!!                
     C                   END                                                    -+!!                
     C                   END                                                    --+!                
      *                                                       !                                     
      * Contôle existence de l'enregistrement.                !                                     
      *                                                       !                                     
     C                   MOVEL     £PERIS        £PERI                             !                
     C                   MOVEL     £NECRS        £NECR                             !                
     C                   MOVEL     £RECRS        £RECR                             !                
     C     KGLI01        CHAIN     AFGLI01L                           50           !                
     C     *IN50         IFEQ      *ON                                          --+!                
     C     £REAF         IFEQ      *ZERO                                        -+!!                
     C                   Z-ADD     $RANG         £REAF                           !!!                
     C                   Z-ADD     3             £MSG                            !!!                
     C                   END                                                    -+!!                
     C                   END                                                    --+!                
      *                                                       !                                     
      * Activation SFLNXTCHG.                                 !                                     
      *                                                       !                                     
     C                   SETON                                        30           !                
      *                                                       !                                     
      * Mise hors fonction indicateur zone option.            !                                     
      *                                                       !                                     
     C                   SETOFF                                       31           !                
      *                                                       !                                     
      * Contrôle des options.                                 !                                     
      *                                                       !                                     
     C                   SELECT                                                 --+!                
      *                                                      !!                                     
      * Enregistrement non trouvé.                           !!                                     
      *                                                      !!                                     
     C     *IN50         WHENEQ    *ON                                            !!                
     C                   SETON                                        31          !!                
      *                                                      !!                                     
      * *BLANK.                                              !!                                     
      *                                                      !!                                     
     C     $OPT          WHENEQ    *BLANK                                         !!                
     C                   SETOFF                                       30          !!                
      *                                                      !!                                     
      * 5=Afficher.                                          !!                                     
      *                                                      !!                                     
     C     $OPT          WHENEQ    ' 5'                                           !!                
     C                   EXSR      àOPT5                                          !!                
      *                                                      !!                                     
      * Option incorrecte.                                   !!                                     
      *                                                      !!                                     
     C                   OTHER                                                    *!                
     C     £REAF         IFEQ      *ZERO                                        -+!!                
     C                   Z-ADD     $RANG         £REAF                           !!!                
     C                   Z-ADD     4             £MSG                            !!!                
     C                   END                                                    -+!!                
     C                   SETON                                        31          !!                
     C                   ENDSL                                                  --+!                
      *                                                       !                                     
      * Mise à jour du sous-fichier.                          !                                     
      *                                                       !                                     
     C                   MOVEL     £PERIS        £PERI                             !                
     C                   MOVEL     £NECRS        £NECR                             !                
     C                   MOVEL     £RECRS        £RECR                             !                
     C     KGLI01        CHAIN     AFGLI01L                           50           !                
     C     *IN50         IFEQ      *OFF                                         -+ !                
     C                   EXSR      àLOAD                                         ! !                
     C                   END                                                    -+ !                
     C                   UPDATE    SF                                              !                
      *                                                       !                                     
      * Lecture de l'enregistrement modifié suivant.          !                                     
      *                                                       !                                     
     C     àRET          COMP      *ON                                    50       !                
     C  N50              READC     SF                                     50       !                
     C                   END                                                    ---+                
      *                                                                                             
      * Réaffichage sur la première anomalie.                                                       
      *                                                                                             
     C     £REAF         IFGT      *ZERO                                        -+                  
     C                   Z-ADD     £REAF         $RANG                           !                  
     C                   END                                                    -+                  
      *                                                                                             
     C                   ENDSR                                                                      
      *                                                                                             
      *=========================================================================                    
      * àOPT5:                                                                                      
      *=========================================================================                    
      *                                                                                             
     C     àOPT5         BEGSR                                                                      
     C                   MOVEL     £NECRS        àNECR                                              
     C                   MOVEL     £RECRS        àRECR                                              
     C                   MOVEL     £PERIS        àPERI                                              
     C                   CALL      'CP4099'                                                         
     C                   PARM                    àSOC                                               
     C                   PARM                    àPERI             6                                
     C                   PARM                    àNECR             5                                
     C                   PARM                    àRECR             5                                
     C                   MOVE      *OFF          àRET                                               
     C     àRET          IFEQ      *OFF                                         -+                  
     C                   MOVE      *BLANK        $OPT                            !                  
     C                   SETOFF                                       30         !                  
     C                   END                                                    -+                  
     C                   ENDSR                                                                      
      *                                                                                             
      *=========================================================================                    
      * àF5: emplissage du sous fichier jusqu'à la page en cours.                                   
      *=========================================================================                    
      *                                                                                             
     C     àF5           BEGSR                                                                      
     C                   Z-ADD     £DEF          £DEF2             4 0                              
     C                   EXSR      àRECH                                                            
     C     *IN26         IFEQ      *OFF                                         --+                 
     C     £EOSF         DOWLT     £DEF2                                        -+!                 
     C     *IN27         ANDEQ     *OFF                                          !!                 
     C                   EXSR      àROLL                                         !!                 
     C                   END                                                    -+!                 
     C                   END                                                    --+                 
     C                   ENDSR                                                                      
      *                                                                                             
      *=========================================================================                    
      * àF15:                                                                                       
      *=========================================================================                    
      *                                                                                             
     C     àF15          BEGSR                                                                      
     C                   ENDSR                                                                      
      *                                                                                             
      *=========================================================================                    
      * àF28:                                                                                       
      *=========================================================================                    
      *                                                                                             
     C     àF28          BEGSR                                                                      
     C                   EXSR      àROLL                                                            
     C                   ENDSR                                                                      
      *                                                                                             
      *=========================================================================                    
      * àF29:                                                                                       
      *=========================================================================                    
      *                                                                                             
     C     àF29          BEGSR                                                                      
     C                   ENDSR                                                                      
      *                                                                                             
      *=========================================================================                    
      * àCVTE: conversion de date.                                                                  
      *=========================================================================                    
      *                                                                                             
     C     àCVTE         BEGSR                                                                      
     C                   MOVEL     £ANN2         £ANN1                                              
     C                   MOVEL     £MOI2         £MOI1                                              
     C                   MOVEL     £JOU2         £JOU1                                              
     C                   ENDSR                                                                      
      *                                                                                             
      *=========================================================================                    
      * àCVTV: conversion de date à envers.                                                         
      *=========================================================================                    
      *                                                                                             
     C     àCVTV         BEGSR                                                                      
     C                   MOVEL     £ANN1         £ANN2                                              
     C                   MOVEL     £MOI1         £MOI2                                              
     C                   MOVEL     £JOU1         £JOU2                                              
     C                   ENDSR                                                                      
      *                                                                                             
      *=========================================================================                    
**                                                                                                  
Message XXXX de type CP1000    non trouvé.                                                          
